(*
    ANTLR v3 grammar written in Grako EBNF syntax.

    This grammar is inspired in, but different one by the one created
    by Terence Parr using ANTLR syntax. It is licensed under the BSD
    License to keep in the spirit of ANTLR.
*)

grammar
    =
    [('lexer'|'parser')] 'grammar' >> name ';'
    [options]
    [tokens]
    {scope}
    {action}
    {rule}+
    $
    ;

options
    =
    'options' >> '{' {option}+ '}' >>
    ;

option
    =
    id '=' >>  (id|string|char|int|'*') ';' >>
    ;

tokens
    =
    'tokens' >> '{' {token}+ '}'
    ;

token
    =
    token_name ['=' >> token_value ] ';' >>
    ;

token_value
    =
    literal
    ;

scope
    =
    'scope' >>
    (
      block ['scope' >> id {',' id} ';' ]
    | id {',' >> id} ';'
    | id block
    )
    ;

action
    =
    '@' >> ('lexer'|'parser'|id)
    ['::' >> id]
    block
    ;

block
    =
    '{' >> {(block|?/[^}]*/?)} '}'
    ;

rule
    =
    ['protected'|'public'|'private'|'fragment']
    id ['!'] [arg]
    ['returns' arg]
    ['trhows' id {',' id}* ]
    [options]
    [scope]
    {action}
    ':' >> alternatives ';' >>
    [exceptions]
    ;

arg
    =
    '[' >>
        {
            arg
        |
            ?/[^\]]*/?
        }*
    ']'
    ;

exceptions
    =
    {'catch' >> arg block}
    ['finally' >> block]
    ;

alternatives
    =
    alternative {'|' >> alternative}
    ;

alternative
    =
    {element}* [rewrite]
    ;

element
    =
      id ('+='| '=') >>  atom
    | gated_predicate
    | semantic_predicate
    | semantic_action
    | syntactic_predicate
    | optional
    | closure
    | positive_closure
    | atom
    ;

gated_predicate
    =
    block '?=>' >>
    ;

semantic_predicate
    =
    block '?' >>
    ;

semantic_action
    =
    block
    ;

syntactic_predicate
    =
    subexp '=>' >>
    ;

optional
    =
    atom '?' >>
    ;

closure
    =
    atom '*' >>
    ;

positive_closure
    =
    atom '+' >>
    ;

atom
    =
    ['^'|'!']
    (
      negative
    | range
    | terminal
    | rule_ref
    | subexp
    | terminal
    | non_terminal
    )
    ;

range
    =
    char '..' >> char
    ;

subexp
    =
    '(' >>
        [options ':' >>]
        alternatives
    ')'
    ;

negative
    =
    '~' >> (non_terminal|subexp)
    ;

non_terminal
    =
      rule_ref
    | token_ref
    ;

terminal
    =
      char
    | string
    | '.' >>
    ;


rewrite
    =
    '->' >> ?/[^|;]*/?
    ;

rule_ref
    =
    lower_name
    ;

token_ref
    =
    upper_name
    ;

token_name
    =
    upper_name
    ;

literal
    =
    id | string | int
    ;


id
    =
    name
    ;

name
    =
    ?/[a-zA-Z]/? NAME_CHARS
    ;

lower_name
    =
    ?/[a-z]/? NAME_CHARS
    ;

upper_name
    =
    ?/[A-Z]/? NAME_CHARS
    ;

NAME_CHARS
    =
    ?/[A-Za-z0-9_]*/?
    ;

char
    =
    string
    ;

string
    =
    STRING
    ;

STRING
    =
    '"' >> @?/([^"\n]|\\")*?/? '"'
    |
    "'" >> @?/([^'\n]|\\')*?/? "'"
    |
    '<<' >> {!'>>' ?/./?} '>>'
    ;

int
    =
    ?/[0-9]+/?
    ;

ESC
    =
    '\' >>
    @(
        '\\'
    |
        '"'
    |
        "'"
    |
        'u' XDIGIT XDIGIT XDIGIT XDIGIT
    |
        ?/[nrtbf>]/?
    )
    ;

XDIGIT
    =
    ?/[a-fA-F0-9]/?
    ;
